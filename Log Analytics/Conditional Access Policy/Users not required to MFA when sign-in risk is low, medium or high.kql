//use this query in azure monitor or log analytics where the logs are stored to determine the impact if the policy was enabled in Entra ID (Azure AD)
//Users not required to MFA when sign-in risk is low, medium or high
let strauthreq = SigninLogs 
    | where ResultType in (50074)
    | where RiskLevelDuringSignIn in ("low","medium","high")
    | where AuthenticationRequirementPolicies !has "riskBasedPolicy"
    | distinct CorrelationId;
SigninLogs
| where RiskState <> "remediated"
| where ResultType in (0)
| where AppDisplayName !in ("Microsoft Authentication Broker","Windows Sign In")
| where RiskLevelDuringSignIn in ("low","medium","high")
| where CorrelationId !in (strauthreq)
| extend authRequirement = tostring(parse_json(AuthenticationRequirementPolicies)[1].requirementProvider)
| where authRequirement <> "riskBasedPolicy" 
| distinct
    ResultType,
    ResultDescription,
    AppDisplayName,
    UserDisplayName,
    UserPrincipalName = tolower(UserPrincipalName),
    UserType,
    RiskLevelAggregated,
    RiskLevelDuringSignIn,
    RiskEventTypes_V2
| summarize
    Applications=make_set(AppDisplayName),
    ["User Risk Level"]=make_set(RiskLevelAggregated), 
    ["Risk Types"]=make_set(RiskEventTypes_V2),
    ["Sign-in Risk Level"] = make_set(RiskLevelDuringSignIn)
    by
    UserDisplayName,
    UserPrincipalName,
    UserType
| project
    ["User Display Name"] = UserDisplayName,
    ["User Principal Name"] = UserPrincipalName,
    ["User Type"] = UserType,
    ["User Risk Level"],
    ["Sign-in Risk Level"],
    ["Risk Types"],
    Applications
