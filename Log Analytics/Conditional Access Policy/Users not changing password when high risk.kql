//use this query in azure monitor or log analytics where the logs are stored to determine the impact if the policy was enabled in Entra ID (Azure AD)
//Users not changing password when high risk
SigninLogs 
| where ResultType == 0  
| where RiskState == "atRisk" and RiskLevelAggregated == 'high' 
| where AppDisplayName <> "Microsoft Authentication Broker" 
| extend authRequirement = tostring(parse_json(AuthenticationRequirementPolicies)[1].requirementProvider) 
| distinct
    ResultType,
    ResultDescription,
    AppDisplayName,
    UserDisplayName,
    UserPrincipalName = tolower(UserPrincipalName),
    UserType,
    RiskLevelAggregated,
    RiskLevelDuringSignIn,
    RiskEventTypes_V2
| summarize
    Applications=make_set(AppDisplayName),
    ["User Risk Level"]=make_set(RiskLevelAggregated), 
    ["Sign-in Risk Level"] = make_set(RiskLevelDuringSignIn),
    ["Risk Types"]=make_set(RiskEventTypes_V2)
    by UserDisplayName, UserPrincipalName, UserType
| project
    ["User Display Name"] = UserDisplayName,
    ["User Principal Name"] = UserPrincipalName,
    ["User Type"] = UserType,
    ["User Risk Level"],
    ["Sign-in Risk Level"],
    ["Risk Types"],
    Applications
