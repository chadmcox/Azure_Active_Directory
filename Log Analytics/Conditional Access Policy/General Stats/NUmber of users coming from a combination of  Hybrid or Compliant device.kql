let excludeapps = pack_array("Windows Sign In","Microsoft Authentication Broker","Microsoft Account Controls V2","Microsoft Intune Company Portal","Microsoft Mobile Application Management");
SigninLogs
| where TimeGenerated > ago(30d) and AppDisplayName !in (excludeapps) and ResultType == 0 and UserType == "Member"
| extend Day = startofday(TimeGenerated)
| extend trustType = tostring(parse_json(DeviceDetail).trustType) 
| extend isCompliant = tostring(parse_json(DeviceDetail).isCompliant) 
| summarize
    ['Total Signin Count']=count(),
    ['Combined']=countif(trustType == "Hybrid Azure AD joined" or isCompliant == "true"),
    ['Both Are Set']=countif(trustType == "Hybrid Azure AD joined" and isCompliant == "true"),
    ['Hybrid Azure AD joined']=countif(trustType == "Hybrid Azure AD joined"),
    ['Compliant']=countif(isCompliant == "true") by Day  
| project Day,HAADJorCompliantPercentage=(todouble(['Combined']) * 100 / todouble(['Total Signin Count'])),
HAADJandCompliant=(todouble(['Both Are Set']) * 100 / todouble(['Total Signin Count']))
| render columnchart
    with (
    kind=unstacked,
    xtitle="Day",
    ytitle="Signin Count",
    title="HAADJ vs Compliant Device Sign-ins per day")
