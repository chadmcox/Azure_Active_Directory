let excludeapps = pack_array("Windows Sign In","Microsoft Authentication Broker","Microsoft Account Controls V2","Microsoft Intune Company Portal","Microsoft Mobile Application Management");
SigninLogs
| where ResultType == 0 and TimeGenerated >= ago(10d) and HomeTenantId == ResourceTenantId  and UserType <> "Guest" and AppDisplayName !in (excludeapps)
| where isnotnull(ConditionalAccessPolicies)
| extend Day = startofday(TimeGenerated)
| mv-apply ca=todynamic(ConditionalAccessPolicies) on (
    summarize condition1 = countif(ca.result in ("success","failure"))
)
| summarize
    ['Total Signin Count']=count(),
    ['No Policy Applied']=countif(condition1 == 0) by Day, AppDisplayName  
| project Day, AppDisplayName ,NoCaAppliedPercentage=(todouble(['No Policy Applied']) * 100 / todouble(['Total Signin Count']))
