let perms =  pack_array("Mail.Read","Mail.Read.Shared","Mail.ReadBasic","Mail.ReadBasic.All","Mail.ReadWrite","Mail.ReadWrite.Shared","Mail.Send", "Mail.Send.Shared","MailboxSettings.Read","MailboxSettings.ReadWrite","email","EWS.AccessAsUser.All","Exchange.Manage");
let operations = pack_array('Add app role assignment to service principal','Remove app role assignment to service principal');
AuditLogs 
| where TimeGenerated > ago (30d)
| where OperationName in (operations)
| extend AppRoleValue = replace_string(tostring(parse_json(parse_json(TargetResources)[0].modifiedProperties)[1].newValue),'"','')
| extend AppRoleDisplayName = tostring(parse_json(parse_json(TargetResources)[0].modifiedProperties)[2].newValue)
| extend ServicePrincipalObjectID = tostring(parse_json(parse_json(TargetResources)[0].modifiedProperties)[5].newValue)
| extend ServicePrincipalName = tostring(parse_json(parse_json(TargetResources)[0].modifiedProperties)[6].newValue)
| extend API = tostring(parse_json(TargetResources)[0].displayName)
| extend InitiatedBy = tostring(InitiatedBy.user.userPrincipalName)
| where AppRoleValue in~ (perms)
| project TimeGenerated, OperationName, InitiatedBy, API, AppRoleValue, AppRoleDisplayName, ServicePrincipalObjectID, ServicePrincipalName
