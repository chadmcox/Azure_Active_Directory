//this will retrieve accounts with multiple bad password attempts
SigninLogs 
| where TimeGenerated > ago(5d) 
| where ResultType == 50126
| summarize lockoutcount = count() by UserPrincipalName, startofday(TimeGenerated)
| where lockoutcount > 5
| sort by lockoutcount

//new query
//get bad password attempts from non trusted locations
SigninLogs 
| where TimeGenerated > ago(5d) 
| where ResultType == 50126
| extend TrustedLocation = tostring(iff(NetworkLocationDetails contains 'trustedNamedLocation', 'trustedNamedLocation','')) 
| extend Country = tostring(LocationDetails.countryOrRegion)
| where TrustedLocation != "trustedNamedLocation"
| summarize badpwdcount = count() by UserPrincipalName, IPAddress, Country, AppDisplayName
| where badpwdcount > 5
| sort by badpwdcount

//new query
//this query looks for bad passwords by users hitting up against exchange online
SigninLogs 
| where TimeGenerated > ago(5d) 
| where ResultType == 50126
| extend TrustedLocation = tostring(iff(NetworkLocationDetails contains 'trustedNamedLocation', 'trustedNamedLocation','')) 
| extend Country = tostring(LocationDetails.countryOrRegion)
| where TrustedLocation != "trustedNamedLocation"
| where AppDisplayName  == "Office 365 Exchange Online"
| summarize badpwdcount = count(),
    usercount = count(UserPrincipalName) by UserPrincipalName, IPAddress, Country, AppDisplayName, ClientAppUsed
| where badpwdcount > 5
| sort by badpwdcount


//new query
//this query will list the accounts being disabled frequently
SigninLogs 
| where TimeGenerated > ago(5d) 
| where ResultType == 50053
| summarize lockoutcount = count() by UserPrincipalName
| where lockoutcount > 5
| sort by lockoutcount
