//https://github.com/reprise99/Sentinel-Queries/blob/main/Azure%20Active%20Directory/Identity-VisualizePasswordvsPasswordless.kql
let timerange=180d;
SigninLogs
| project TimeGenerated, AuthenticationDetails
| where TimeGenerated > ago (timerange)
| extend AuthMethod = tostring(parse_json(AuthenticationDetails)[0].authenticationMethod)
| where AuthMethod != "Previously satisfied"
| summarize
    Password=countif(AuthMethod == "Password"),
    Passwordless=countif(AuthMethod in ("FIDO2 security key", "Passwordless phone sign-in", "Windows Hello for Business"))
    by startofweek(TimeGenerated)
| render columnchart
    with (
    kind=unstacked,
    xtitle="Week",
    ytitle="Signin Count",
    title="Password vs Passwordless signins per week")






// chart daily windows hello for busines
AuditLogs
| where TimeGenerated > ago(30d)
| where OperationName in ("Add Windows Hello for Business credential")
| summarize Count=count()by OperationName, startofday(TimeGenerated)
| render columnchart with (kind=unstacked, title="whatevber", xtitle="day")
